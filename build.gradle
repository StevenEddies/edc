plugins {
	id 'com.palantir.docker' version '0.32.0' apply false
	id 'com.palantir.docker-compose' version '0.32.0' apply false
	id 'com.github.node-gradle.node' version '3.1.1' apply false
}

allprojects {
    apply plugin: 'uk.me.eddies.apps.edc.gradleconvention.formatter'
	version = project.findProperty('buildVersion') ?: '0.0-dev.0'
	minorVersion = (version =~ /^(.*)\.\d+$/)[0][1]
	group = 'uk.me.eddies'
}

project(':edc-backend') {

	apply plugin: 'java'
	apply plugin: 'application'
	apply plugin: 'com.palantir.docker'
	
	sourceCompatibility = 17
	targetCompatibility = 17

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.slf4j:slf4j-api:1.7.32'
		implementation 'io.dropwizard:dropwizard-core:2.0.28'
		implementation 'io.dropwizard:dropwizard-auth:2.0.28'
		implementation 'at.favre.lib:bcrypt:0.9.0'
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
		testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
		testImplementation 'org.hamcrest:hamcrest:2.2'
		testImplementation 'org.awaitility:awaitility:4.1.1'
		testImplementation 'io.dropwizard:dropwizard-testing:2.0.28'
		testImplementation 'io.dropwizard:dropwizard-client:2.0.28'
		testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.10'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
	}

	tasks.withType(Test) {
		useJUnitPlatform()
		ignoreFailures = true
	}

	application {
		mainClass = 'uk.me.eddies.apps.edc.backend.application.EdcBackendApplication'
	}
	
	run {
		args 'server'
		args project.findProperty('configFile') ?: file('src/test/resources/uk/me/eddies/apps/edc/backend/application/testconfig.yaml')
	}
	
	docker {
		name "steveneddies/edc-backend:${version}"
		tag minorVersion, "steveneddies/edc-backend:${minorVersion}"
		dockerfile file('src/main/docker/Dockerfile')
		files tasks.installDist.outputs
		pull true
	}
	
	dockerfileZip {
		archiveFileName = 'edc-backend.zip'
		destinationDirectory = file('build/docker-zip')
	}
}

project(':edc-config-util') {

	apply plugin: 'java'
	apply plugin: 'application'
	apply plugin: 'com.palantir.docker'
	
	sourceCompatibility = 17
	targetCompatibility = 17

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.slf4j:slf4j-api:1.7.32'
		implementation 'at.favre.lib:bcrypt:0.9.0'
		runtimeOnly 'ch.qos.logback:logback-classic:1.2.10'
	}

	application {
		mainClass = 'uk.me.eddies.apps.edc.configutil.application.EdcConfigUtil'
	}
	
	docker {
		name "steveneddies/edc-config-util:${version}"
		tag minorVersion, "steveneddies/edc-config-util:${minorVersion}"
		dockerfile file('src/main/docker/Dockerfile')
		files tasks.installDist.outputs
		pull true
	}
	
	dockerfileZip {
		archiveFileName = 'edc-config-util.zip'
		destinationDirectory = file('build/docker-zip')
	}
}

project('edc-frontend') {

	apply plugin: 'com.github.node-gradle.node'
	apply plugin: 'com.palantir.docker'
	
	node {
		version = '14.17.0'
		npmVersion = '6.14.13'
		download = true
	}
	
	tasks.register('angularBuild', NpxTask) {
		group = 'angular'
		description = 'Use the Angular CLI to build the web app.'
		dependsOn npmInstall
		command = 'ng'
		args = [ 'build' ]
		inputs.files 'package.json', 'package-lock.json', 'angular.json', 'tsconfig.json', 'tsconfig.app.json'
		inputs.dir 'src'
		inputs.dir fileTree('node_modules').exclude('.cache')
		outputs.dir 'build/angular'
	}
	
	docker {
		name "steveneddies/edc-frontend:${version}"
		tag minorVersion, "steveneddies/edc-frontend:${minorVersion}"
		dockerfile file('docker/Dockerfile')
		copySpec.from (tasks.angularBuild.outputs) {
			into 'app'
		}
		files 'nginx/ng.conf'
		pull true
	}
	
	dockerfileZip {
		archiveFileName = 'edc-frontend.zip'
		destinationDirectory = file('build/docker-zip')
	}
}

project('edc-deploy') {

    apply plugin: 'base'
	apply plugin: 'com.palantir.docker-compose'
	
	dependencies {
		docker project(path: ':edc-backend', configuration: 'docker')
		docker project(path: ':edc-frontend', configuration: 'docker')
	}
	
	generateDockerCompose {
		mustRunAfter clean
	}
	
	tasks.register('processResources', Copy) {
		group = 'dist'
		description = 'Copies resources to the dist directory.'
		from 'src/main/resources'
		into 'build/dist/edc'
		mustRunAfter clean
	}
	
	dockerCompose {
		template 'src/main/docker/docker-compose.yaml.template'
		dockerComposeFile 'build/dist/edc/docker-compose.yaml'
	}
	
	tasks.register('assembleDist') {
		group = 'dist'
		description = 'Assembles the project.'
		dependsOn generateDockerCompose, processResources
	}
	
	tasks.register('packageDist', Zip) {
		group = 'dist'
		description = 'Creates a zip package of the project.'
		dependsOn assembleDist
		archiveFileName = "edc-${project.version}.zip"
		destinationDirectory = file('build/dist')
		from file('build/dist/edc')
		into 'edc'
	}
	
	dockerComposeUp {
		dependsOn assembleDist
		dependsOn ':edc-backend:docker'
		dependsOn ':edc-frontend:docker'
	}
}

apply plugin: 'uk.me.eddies.gradleplugins.github-release'

githubRelease {
	owner = 'StevenEddies'
	repo = 'edc'
	token = project.findProperty('uk.me.eddies.github.token') ?: ''
	tagName = "v${version}"
	name = "Version ${version}"
	prerelease = version =~ /dev/
	draft = true
	assets = [ "edc-deploy/build/dist/edc-${project.version}.zip" ]
}

tasks.named('githubRelease') {
	dependsOn ':edc-deploy:packageDist'
}

tasks.register('release') {
	group = 'dist'
	description = 'Makes a full release via Docker Hub and Github.'
	dependsOn ':edc-backend:dockerPush'
	dependsOn ':edc-frontend:dockerPush'
	dependsOn ':edc-config-util:dockerPush'
	dependsOn ':githubRelease'
}
